plugins {
    id "com.moowork.node" version "1.3.1"
}

node {
    version = '12.18.3'
    npmVersion = '6.14.7'
    download = true
}

task compileTypeScript(type: NodeTask, dependsOn: npmInstall) {
    script = file('node_modules/typescript/bin/tsc')
    args = ['-p', 'src/main/ts', '--outDir', './build/node_modules/main/']
}

task copyRawFiles(type: Copy) {
    from 'src/main/raw'
    from 'CHANGELOG.markdown'
    into './build/node_modules/main'
}

task assemble(dependsOn: [compileTypeScript, copyRawFiles])

task test(type: NodeTask, dependsOn: compileTypeScript) {
    script = file('node_modules/mocha/bin/mocha')
    args = ['-u', 'qunit', '-R', 'tap', './build/node_modules/main/*.test.js']
}

task check(dependsOn: test)

task build(dependsOn: [assemble, check])

task clean(type: Delete) {
    delete './build'
}

task npmPack(type: NpmTask, dependsOn: build) {
    args = ['pack', './build/node_modules/main']
}

task npmPublish(type: NpmTask, dependsOn: build) {
    // See: https://docs.npmjs.com/cli/publish
    args = ['publish', './build/node_modules/main']
}
