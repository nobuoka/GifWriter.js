plugins {
    id "com.moowork.node" version "1.3.1"
}

node {
    version = '12.18.3'
    npmVersion = '6.14.7'
    download = true
}

task compileTypeScript(type: NodeTask, dependsOn: npmInstall) {
    script = file('node_modules/typescript/bin/tsc')
    args = ['-p', 'src/ts', '--outDir', './build']
}

task copyRawFiles(type: Copy) {
    from 'src/raw'
    from 'CHANGELOG.markdown'
    into './build'
}

task assemble(dependsOn: [compileTypeScript, copyRawFiles])

task test(type: NodeTask, dependsOn: compileTypeScript) {
    script = file('node_modules/istanbul/bin/istanbul')
    args = ['cover', 'node_modules/mocha/bin/_mocha', '--', '-u', 'qunit', '-R', 'tap', './build/*.test.js']
}

task check(dependsOn: test)

task build(dependsOn: [assemble, check])

task clean(type: Delete) {
    delete './build'
}

task npmPack(type: NpmTask, dependsOn: build) {
    args = ['pack', './build']
}

task npmPublish(type: NpmTask, dependsOn: build) {
    // See: https://docs.npmjs.com/cli/publish
    args = ['publish', './build']
}
